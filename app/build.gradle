apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'



def archVersion = "1.1.1"
def designSupportVersion = "28.0.0"
def supportVersion = "28.0.0"
def daggerVersion = "2.16"
def rxJavaVersion = "2.1.14"
def rxAndroidVersion = "2.0.2"
def rxBindingsVersion = "2.0.0"
def firebaseVersionCore = "16.0.5"
def firebaseVersionConfig = "16.1.0"
def playServicesVersionVision = "17.0.2"
def playServicesVersionDrive = "16.0.0"
def playServicesVersionAuth = "16.0.1"
def retrofitVersion = "2.4.0"
def shockUtilVersion = "4.0.1"
def crashlyticsVersion = "2.9.8"
def glideVersion = "4.8.0"
def lottieVersion = "2.5.6"
def rxImagePickerVersion = "2.2.0"

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("./keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "at.shockbytes.dante"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 20
        versionName "3.2"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        debug {
            // default
        }
        proguard_debug {
            // default
        }
        release {
            keyAlias keystoreProperties.getProperty('KEY_ALIAS')
            keyPassword keystoreProperties.getProperty('KEY_PASSWORD')
            storeFile file(keystoreProperties.getProperty('STORE_FILE'))
            storePassword keystoreProperties.getProperty('STORE_PASSWORD')
        }
    }
    buildTypes {
        release {
            useProguard true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        // Use this build type in order to avoid, that release build type will be signed with the release key
        proguard_debug {
            initWith(debug)
            useProguard true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            //applicationIdSuffix ".debug"
            debuggable true
        }
    }
    lintOptions {
        disable 'RestrictedApi'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}
kapt {
    arguments {
        arg("realm.ignoreKotlinNullability", true)
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:design:$designSupportVersion"
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:palette-v7:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:support-compat:$supportVersion"
    implementation "com.android.support:preference-v7:$supportVersion"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:multidex:1.0.3'

    implementation "android.arch.lifecycle:extensions:$archVersion"
    implementation "android.arch.lifecycle:reactivestreams:$archVersion"
    kapt "android.arch.lifecycle:compiler:$archVersion"
    implementation "com.google.android.gms:play-services-vision:$playServicesVersionVision"
    implementation "com.google.android.gms:play-services-drive:$playServicesVersionDrive"
    implementation "com.google.android.gms:play-services-auth:$playServicesVersionAuth"
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
        transitive = true
    }
    implementation "com.google.firebase:firebase-core:$firebaseVersionCore"
    implementation "com.google.firebase:firebase-config:$firebaseVersionConfig"

    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "com.github.shockbytes:ShockUtil:$shockUtilVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation "com.airbnb.android:lottie:$lottieVersion"

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'

    implementation 'net.danlew:android.joda:2.9.9'
    implementation 'ru.bullyboo.view:circleseekbar:1.0.3'
    implementation "com.leinardi.android:speed-dial:2.0.1"
    implementation 'com.mlsdev.rximagepicker:library:2.1.5'
    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

    implementation "com.github.qingmei2:rximagepicker:$rxImagePickerVersion"
    implementation "com.github.qingmei2:rximagepicker_support:$rxImagePickerVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBindingsVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxBindingsVersion"

    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation 'com.android.support:recyclerview-v7:28.0.0'


    // Test libraries
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.1.0'
    testImplementation 'org.mockito:mockito-core:2.16.0'
    androidTestImplementation 'androidx.test:core:1.1.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.ext:truth:1.1.0'
    androidTestImplementation 'com.google.truth:truth:0.42'
}

apply plugin: 'com.google.gms.google-services'
